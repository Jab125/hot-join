plugins {
	id 'fabric-loom' version '1.7-SNAPSHOT'
	id 'maven-publish'
	id 'org.moddedmc.wiki.toolkit' version '0.2.4'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
	exclusiveContent {
		forRepository {
			maven {
				name = "Modrinth"
				url = "https://api.modrinth.com/maven"
			}
		}
		filter {
			includeGroup "maven.modrinth"
		}
	}

	// libsdl4j
	maven {
		url "https://maven.isxander.dev/releases"
	}
	maven {
		url "https://maven.terraformersmc.com/releases"
	}
}

// for moddedmc.org
wiki {
	docs {
		// The name of the object (examplemod) should match the registered wiki project ID (if it exists).
		hotjoin {
			// The path to the folder containing the documentation metadata file (sinytra-wiki.json)
			root = file('docs')
		}
	}
}

fabricApi {
	configureDataGeneration()
}

loom {
	mods {
		main {
			sourceSet sourceSets.main
		}
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation "maven.modrinth:concentration:MXVGS9Kc" // 2.0.1-fabric for 1.21.1
	modImplementation "maven.modrinth:auth-me:8.0.0+1.21"
	modCompileOnly "maven.modrinth:legacy4j:1.21-1.7.4+fabric"
	api "dev.isxander:libsdl4j:3.693c75e-36" // Legacy4J needs this
	modImplementation "dev.isxander:controlify:2.0.0-beta.14+1.21-fabric", {
		exclude group: "me.flashyreese.mods", module: "reeses-sodium-options"
		exclude group: "com.terraformersmc", module: "modmenu"
		exclude group: "net.lenni0451", module: "Reflect"
		exclude group: "maven.modrinth", module: "simple-voice-chat"
		exclude group: "maven.modrinth", module: "fancymenu"
		exclude group: "maven.modrinth", module: "iris"
		exclude group: "maven.modrinth", module: "sodium"
		exclude group: "org.anarres", module: "jcpp"
		exclude group: "io.github.douira", module: "glsl-transformer"
		exclude group: "maven.modrinth", module: "immediatelyfast"
	}

	modRuntimeOnly "com.terraformersmc:modmenu:11.0.3"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}